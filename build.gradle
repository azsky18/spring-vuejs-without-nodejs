plugins {
	id 'org.springframework.boot' version '2.2.4.RELEASE'
	id 'io.spring.dependency-management' version '1.0.9.RELEASE'
	id 'io.franzbecker.gradle-lombok' version '3.2.0'
	id 'com.ewerk.gradle.plugins.querydsl' version '1.0.10'
	id 'java'
	id 'eclipse'
}

group = 'com.ingyeo'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '1.8'

configurations {
	developmentOnly
	runtimeClasspath {
		extendsFrom developmentOnly
	}
	compileOnly {
		extendsFrom annotationProcessor
	}
}

repositories {
	mavenCentral()
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
	implementation 'org.springframework.boot:spring-boot-starter-web'
	implementation 'com.querydsl:querydsl-core'
	implementation 'com.querydsl:querydsl-jpa'
	implementation 'com.querydsl:querydsl-apt'
	implementation 'com.google.guava:guava:28.1-jre'
	implementation 'org.apache.commons:commons-lang3:3.9'
	compileOnly 'org.projectlombok:lombok'
	developmentOnly 'org.springframework.boot:spring-boot-devtools'
	runtimeOnly 'com.h2database:h2'
	annotationProcessor 'org.projectlombok:lombok'
	testImplementation('org.springframework.boot:spring-boot-starter-test') {
		exclude group: 'org.junit.vintage', module: 'junit-vintage-engine'
	}
}

test {
	useJUnitPlatform()
}

querydsl {
    library = 'com.querydsl:querydsl-apt'
    jpa = true
}

compileQuerydsl {
    options.annotationProcessorPath = configurations.querydsl
    group = 'querydsl'
    description = 'Creates the Qeurydsl classes.' 
}

configurations {
	querydsl.extendsFrom compileClasspath
}

eclipse {
    classpath {
        file {
            whenMerged {
                def src = entries.find { it.path == 'src/querydsl/java' }
                src.output = 'bin/main'
            }
        }
    }
}